os := "fcos"

scos_base_img:= "quay.io/okd/scos-content@sha256:3813e6608a999756931d3d621932af9662860e71a552b2670f9fe320bf0d3585"
# Tag: quay.io/trusted-execution-clusters/fedora-coreos:42.20251012.2.0
fcos_base_img:="quay.io/trusted-execution-clusters/fedora-coreos@sha256:6997f51fd27d1be1b5fc2e6cc3ebf16c17eb94d819b5d44ea8d6cf5f826ee773"

scos_img:= "quay.io/trusted-execution-clusters/scos"
fcos_img:= "quay.io/trusted-execution-clusters/fcos"

fcos_os:= "fedora-coreos"
scos_os:= "rhcos"

fcos_label:= "fedora-coreos"
scos_label:= "centos-stream-coreos"

fcos_config:= "https://github.com/coreos/fedora-coreos-config"
scos_config:= "https://github.com/coreos/rhel-coreos-config.git"

base := if os == "scos" { scos_base_img } else { fcos_base_img }
image := if os == "scos" { scos_img } else { fcos_img }
os_name := if os == "scos" { scos_os } else { fcos_os }
label := if os == "scos" { scos_label } else { fcos_label }
archive := os + ".ociarchive"

config := if os == "scos" { scos_config } else { fcos_config }
full_name := if os == "scos" { "centos-stream-coreos" } else { "fedora-coreos" }

build:
    sudo podman build --no-cache --build-arg BASE={{base}} --build-arg COM_COREOS_OSNAME={{label}} -t {{image}} -f Containerfile .

oci-archive:
    sudo skopeo copy containers-storage:{{image}} oci-archive:{{archive}}

# Reusable cosa function definition
cosa_function := '''
    #!/usr/bin/env bash
    cosa() {
        env | grep COREOS_ASSEMBLER || true

        # Default container image
        COREOS_ASSEMBLER_CONTAINER_LATEST="quay.io/coreos-assembler/coreos-assembler:latest"
        sudo podman pull $COREOS_ASSEMBLER_CONTAINER_LATEST

        set -ex
        sudo podman run --rm -ti --security-opt=label=disable --privileged -u 0 \
				--network host \
            -v=${PWD}:/srv/ --device=/dev/kvm --device=/dev/fuse \
            --tmpfs=/tmp -v=/var/tmp:/var/tmp --name=cosa \
            ${COREOS_ASSEMBLER_CONFIG_GIT:+-v=$COREOS_ASSEMBLER_CONFIG_GIT:/srv/src/config/:ro} \
            ${COREOS_ASSEMBLER_GIT:+-v=$COREOS_ASSEMBLER_GIT/src/:/usr/lib/coreos-assembler/:ro} \
            ${COREOS_ASSEMBLER_ADD_CERTS:+-v=/etc/pki/ca-trust:/etc/pki/ca-trust:ro} \
            ${COREOS_ASSEMBLER_CONTAINER_RUNTIME_ARGS} \
            ${COREOS_ASSEMBLER_CONTAINER:-$COREOS_ASSEMBLER_CONTAINER_LATEST} "$@"
    }
'''

init:
    #!/usr/bin/env bash
    {{cosa_function}}
    mkdir -p cache
    cp {{archive}} cache/{{archive}}
    cd cache
    cosa init --force {{config}}
    cosa import oci-archive:/srv/{{archive}}

build-qemu:
    #!/usr/bin/env bash
    {{cosa_function}}
    cd cache
    cosa osbuild qemu

kubevirt:
    #!/usr/bin/env bash
    {{cosa_function}}
    cd cache
    cosa osbuild kubevirt
